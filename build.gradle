import com.axelor.gradle.support.ChangelogSupport

plugins {
	id 'com.axelor.app'
}

apply from: 'version.gradle'
apply plugin: ChangelogSupport

try {
    tasks.named('spotlessCheck')
} catch (UnknownTaskException ignored) {
    apply from: './gradle/style.gradle'
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(11)
	}
}

group = "com.axelor.apps"

axelor {
	title "Axelor :: Mattermost"
	description "Axelor Mattermost"
}

changelog {
  version = baseVersion
  output.set(file('CHANGELOG.md'))
  inputPath.set(file('changelogs/unreleased'))
  types.set(['Feature', 'Change', 'Deprecate', 'Remove', 'Fix', 'Security'])
  header.set("${version.get()} (${new Date().format('yyyy-MM-dd')})")
}

dependencies {
    if (findProject(':modules:axelor-project') != null) {
        api project(':modules:axelor-project')
    } else {
        implementation "com.axelor.apps:axelor-project:${aosVersion}"
    }
}

apply plugin: 'maven-publish'

ext {
	mavenUsername = project.findProperty( 'axelorMavenUsername' )
	mavenPassword = project.findProperty( 'axelorMavenPassword' )
}

publishing {
	repositories {
		maven {
			def repoPath = project.hasProperty("finalRelease")
					? "maven-releases"
					: "maven-snapshots";
			name = "maven"
			url = "https://repository.axelor.com/nexus/repository/" + repoPath
			credentials {
				username = project.mavenUsername
				password = project.mavenPassword
			}
		}
	}
}
